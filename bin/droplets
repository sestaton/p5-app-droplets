#!perl

use 5.010;
use strict;
use warnings;
use File::Basename;
use App::Droplets;
use Getopt::Long;
use Pod::Usage;

our $VERSION = '0.07.1';
my %opt;

my @serverids;
GetOptions(\%opt,
    'size|s=s',
    'region|r=s',
    'servername|n=s',
    'imageid|iid=i',
    'serverid|sid=s{1,}' => \@serverids,
    'configfile|c=s',
    'available|a=s',
    'username|u=s',
    'running',
    'destroy',
    'destroyall',
    'create',
    'configure',
    'version|v',
    'login|l',
    'logon',
    'help|h',
    'man|m',
);

pod2usage( -verbose => 2 ) if $opt{man};
usage() and exit(0) if $opt{help};
say $VERSION and exit(0) if $opt{version};

unless (%opt) {
    say STDERR "\nERROR: Command line not parsed correctly. Please check arguments. Exiting.";
    usage();
    exit(1);
}

$opt{serverid} = \@serverids;
my $drop_cli = App::Droplets->new( opts => \%opt );
$drop_cli->run;

exit;
#
# methods
#
sub usage {
    my $script = basename($0);
  print STDERR <<END

USAGE: $script [-s] [-r] [-u] [-n] [-iid] [-sid] [-c] [-a] [-l] [-h]
               [--droplets] [--create] [--destroy] [--destroyall]       

Options:
    -s|size          :    The size ID requested for creating a new Droplet.
    -r|region        :    The region ID (i.e, 'slug' as given by '--available regions' option)
                          requested for creating a new Droplet.
    -n|servername    :    The name for the instance (Default: a UUID string).
    -iid|image       :    The image ID requested for creating a new Droplet.
    -sid|serverid    :    The server ID to log onto, or destory (when combined with '--destroy', multiple IDs may be given).
    -c|configfile    :    The DigitalOcean configuration file.
    -a|available     :    Given an argument of either 'sizes', 'regions', or 'images', print
                          all of the available options for that choice.
    -l|login         :    If given with other options, will log on to the created Droplet.
    --logon          :    Alias for --login option. Both work the same. This is to avoid having to remember the exact name.
    -u|username      :    Combined with --login, the username to use for logging on to 
                          the server given by --serverid (Default: root).
    --running        :    Print all running Droplets.
    --create         :    Create a droplet with defaults or given parameters
                          (Defaults: size => 512mb, region => San Francisco, OS => CentOS 7). 
    --destroy        :    Given an image ID, this option will destroy the Droplet.
    --destroyall     :    Destroy all of your running droplets.
    -v|version       :    Print the version and exit.
    -h|help          :    Print usage statement.
    -m|man           :    Print full documentation.

END
}

__END__

=head1 NAME 
                                                                       
droplets - Create, destroy, inspect, and log on to your droplets from the command line

=head1 SYNOPSIS    

  ## get the available image sizes
  droplets --available sizes

  # get the available distributions
  droplets --available images
  Distribution	ID	        Name
  CoreOS	12789350	723.3.0 (beta)
  CentOS	6372321	        5.10 x64
  CentOS	6372425	        5.10 x32
  Debian	6372581	        6.0 x64
  Debian	6372662	        6.0 x32
  Fedora	9640922   	21 x64
  FreeBSD	10144573	10.1
  Ubuntu	10321756	12.04.5 x64
  Ubuntu	10321777	12.04.5 x32
  Debian	10322059	7.0 x64
  Debian	10322378	7.0 x32
  CentOS	10322623	7 x64
  Fedora	12065782	22 x64
  Ubuntu	12658446	15.04 x64
  Ubuntu	12660649	15.04 x32
  Debian	12778278	8.1 x64
  Debian	12778337	8.1 x32
  CoreOS	13068283	723.3.0 (stable)
  Ubuntu	13089493	14.04 x64
  Ubuntu	13089823	14.04 x32

  ## Create a droplet (in this case, Fedora 22)
  droplets --imageid 12065782 --create

  ## Check all running droplets
  droplets --running

  ## login to server as root
  droplets --login --serverid 6588509

  ## login to server as user 'evan'
  droplets --login --username evan --serverid 6588509

  ## destroy one specific droplet
  droplets --destroy --serverid 6588509

  ## destroy numerous droplets in one go
  droplets --destroy --serverid 6588509 6588510 6588511 

  ## Create a droplet, with defaults, and log on to it
  droplets --create --login

  ## Destroy all running droplets
  droplets --destroyall

=head1 DESCRIPTION
 
 This is a command-line tool for DigitalOcean's REST API called droplets. After setting up an API key on the DigitalOcean website, you can check on all your existing droplets, query the available droplet sizes and prices by region, create new droplets, or log on to droplets (either existing droplets or ones you just created) without leaving the command line.     

=head1 LICENSE
 
The MIT License should included with the project. If not, it can be found at: http://opensource.org/licenses/mit-license.php

Copyright (C) 2015-2016 S. Evan Staton
 
=head1 AUTHOR 

S. Evan Staton                                                

=head1 CONTACT
 
statonse at gmail dot com

=head1 OPTIONS

=over 2

=item -s, --size

 The size ID requested for creating a new Droplet.

=item -r, --region

 The region ID requested for creating a new Droplet.

=item -n, --servername

 The name for the instance (Default: a UUID string).

=item -iid, --image

 The image ID requested for creating a new Droplet.

=item -sid, --serverid

 The server ID to log onto, or destory.

=item -c, --configfile

 The DigitalOcean configuration file. This option will likely be deprecated.

=item -a, --available

 Given an argument of either 'sizes', 'regions', or 'images', 
 print all of the available options for that choice.

=item -l, --login

 If given with other options, will log on to the created Droplet.

=item --logon

 This option has the same behavior as --login and it exists to avoid accidently typing "--logon" and
 having to then look up the name of the option. 

=item -u, --username

 Combined with --login, the username to use for logging on to the 
 server specified by --serverid (Default: root).

=item --running

 Print all running Droplets.

=item --create

 Create a droplet with defaults or given parameters.

 Defaults: size   => 512mb
           region => San Francisco
           OS     => CentOS 7 

=item --destroy

 Given an image ID, this option will destroy the Droplet.

=item --destroyall

 Destroy all of your running droplets.

=item -v, --version

 Get the program version and exit.

=item -h, --help

  Print a usage statement. 

=item -m, --man
  
  Print the full documentation.

=back

=cut
